/*
 * Copyright (c) 2016 Open Baton (http://www.openbaton.org)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

ext {
    debianApplicationVersionFull = "${applicationVersionFull}".replaceAll("-", ".")

    baseName = 'openbaton'
    componentName = "nfvo"
    fullName = "${baseName}-${componentName}"
    versionName = "${version}".replaceAll("-SNAPSHOT", "")
    propertiesDir = '/etc/openbaton'
    baseBinaryDir = '/usr/lib/openbaton'
    jarSubpathName = "${componentName}".replaceAll("-", "/")
}

task logDebianPackageJavaDependency() {
    // print the logs about the correct java dependency
    doLast() {
        if (project.hasProperty('javaVersion') && (javaVersion == "7")) {
            println " * Adding debian package dependency to Java 7"
        }
        else {
            println " * Adding debian package dependency to Java 8"
        }
    }
}

task makeDeb(type: Deb, dependsOn: [jar, build, logDebianPackageJavaDependency]) {
    packageName = (applicationRelease) ? "${fullName}" : "${fullName}-nightly"
    version = debianApplicationVersionFull
    //release = 1
    priority = 'optional'
    arch = 'all'
    maintainer = 'Open Baton Team <dev@openbaton.org>'
    vendor = 'Open Source (Implemented by Fraunhofer FOKUS and TUB)'
    license = 'Apache 2.0 License'
    url = 'http://openbaton.github.io/index.html'
    summary = 'Open Baton NFV Orchestrator (NFVO)'
    packageDescription = 'The Network Function Virtualization Orchestrator (NFVO) is a component of the Open Baton project which is an ETSI compliant implementation of a NFV Management and Orchestration (MANO) System'

    preInstall file('gradle/gradle/scripts/debian/preinst')
    postInstall file('gradle/gradle/scripts/debian/postinst')
    preUninstall file('gradle/gradle/scripts/debian/prerm')
    postUninstall file('gradle/gradle/scripts/debian/postrm')

    // set the correct java dependency
    if (project.hasProperty('javaVersion') && (javaVersion == "7")) {
        requires('openjdk-7-jre')
    }
    else {
        requires('openjdk-8-jre')
    }

    requires('rabbitmq-server')
    // the flag 4 is for openbaton-plugin-vimdriver-test greater than 4.0.0
    requires('openbaton-plugin-vimdriver-test', '4.0.0', 4)

    // Copy files from "from path" to "into path" 
    from("etc/keystore.p12") 			                                { into "${propertiesDir}" }                             // keystore file
    from("main/src/main/resources/application.properties") {                                                                    // properties file
        into "${propertiesDir}"
        rename("application.properties", "${fullName}.properties")
    }
    from(jar.outputs.files) {                                                                                                   // jar file
        into "${baseBinaryDir}/${jarSubpathName}"
        rename("(.*)","${fullName}-${version}.jar")
    }
    from("gradle/gradle/scripts/${fullName}") 	                        { into "/usr/bin/" }                                    // executable to run the Component
    from("gradle/gradle/scripts/${fullName}") 	                        { into "/etc/init.d/" }                                 // executable to run the Component as a service (SysV)
    from("gradle/gradle/scripts/${fullName}.conf") 	                    { into "/etc/init/" }                                   // conf file to run the Component as a service (Upstart)
    from("gradle/gradle/scripts/systemd/${fullName}.service") 	        { into "/usr/lib/systemd/system/" }                     // executable to run the Component as a service (Systemd)
    from("gradle/gradle/scripts/systemd/${fullName}.env") 	            { into "/usr/lib/openbaton/systemd/env/" }              // environment file to run the Component as a service (Systemd)
    from("gradle/gradle/scripts/systemd/${fullName}.preexecstart") 	    { into "/usr/lib/openbaton/systemd/preexecstart/" }     // preexecstart file to run the Component as a service (Systemd)
}

