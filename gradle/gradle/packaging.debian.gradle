/*
 * Copyright (c) 2015 Fraunhofer FOKUS. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


ext {
    debianDistribution = (applicationRelease) ? "release" : "snapshot"
    debianVersionExtension = ("snapshot".equals(debianDistribution)) ? "~${applicationVersionDebian}" : ""
    debianApplicationVersionFull = "${applicationVersionFull}".replaceAll("-", ".") + debianVersionExtension
    openbatonInstallDir = "/opt/openbaton"

}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.6'
    }
}

apply plugin: 'os-package'

task openbatonDeb(type: Deb, dependsOn: [jar, installDist]) {
    packageName = 'openbaton'
    version = debianApplicationVersionFull
    release '1'
    os = LINUX

    configurationFile('/etc/openbaton/openbaton.properties')
    postInstall file('gradle/gradle/scripts/debian/postinst')

    into openbatonInstallDir

    from(jar.outputs.files) { into 'lib'  }
    from('build/install/openbaton/lib') { into 'lib' }
    from('plugins/vim-drivers/') { into 'lib' }
    from('plugins/monitoring/') { into 'lib' }

    from('build/install/openbaton/bin/openbaton') { into 'bin' }
    from('etc/openbaton.properties') { into '/etc/openbaton/' }
    from('gradle/gradle/scripts/openbaton.sh')
    from('gradle/gradle/scripts/openbaton.conf') { into '/etc/init/' }

    link('/etc/init.d/openbaton', '/lib/init/upstart-job')
    link('/usr/bin/openbaton','/opt/openbaton/bin/openbaton')

}
